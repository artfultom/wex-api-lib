apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.artfultom"
archivesBaseName = "wexapi"
version = "2.0.0-SNAPSHOT"

repositories {
    jcenter()
}

dependencies {
    api 'org.apache.httpcomponents:httpclient:4.5.3'
    api 'com.fasterxml.jackson.core:jackson-core:2.9.2'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.9.2'
    api 'com.fasterxml.jackson.core:jackson-databind:2.9.2'
    api 'com.pusher:pusher-java-client:1.5.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.3'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.3'
}

test {
    systemProperty 'key', System.getProperty('key')
    systemProperty 'secret', System.getProperty('secret')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/content/repositories/releases/") {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }

            pom.project {
                name 'wex-api-lib'
                packaging 'jar'
                description 'API library for wex.com (btc-e.com / btc-e.nz / wex.nz)'
                url 'https://github.com/artfultom/wex-api-lib'

                scm {
                    connection 'scm:git:https://github.com/artfultom/wex-api-lib.git'
                    developerConnection 'scm:git:https://github.com/artfultom/wex-api-lib.git'
                    url 'https://github.com/artfultom/wex-api-lib.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://raw.githubusercontent.com/artfultom/wex-api-lib/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'artfultom'
                        name 'Andrey Vasilyev'
                        email 'artfultomcat@gmail.com'
                    }
                }
            }
        }
    }
}